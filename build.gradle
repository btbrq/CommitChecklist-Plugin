plugins {
    id 'org.jetbrains.intellij.platform' version '2.2.1'
    id 'java'
    id 'groovy'

}


group 'brq.intellij.plugins'
version '1.3.1'

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    testImplementation("org.codehaus.groovy:groovy-all:2.5.14")
    testImplementation("org.spockframework:spock-core:1.2-groovy-2.5")
    intellijPlatform {
        intellijIdeaCommunity('2024.3.2')
        bundledPlugin("com.intellij.java")
        bundledPlugin("com.intellij.modules.json")
        instrumentationTools()
    }
}

patchPluginXml {
    if (System.getenv("CHANGE_NOTES_PATH")) {
        changeNotes.set(file(System.getenv("CHANGE_NOTES_PATH")).text)
    }
    untilBuild.set(null)
    sinceBuild.set("213.1")
}

signPlugin {
    if (System.getenv("CERTIFICATE_CHAIN_PATH") && System.getenv("PRIVATE_KEY_PATH")) {
        certificateChain.set(file(System.getenv("CERTIFICATE_CHAIN_PATH")).text)
        privateKey.set(file(System.getenv("PRIVATE_KEY_PATH")).text)
        password.set(System.getenv("PRIVATE_KEY_PASSWORD"))
    }
}

publishPlugin {
    token.set(System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken"))
}

